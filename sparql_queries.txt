### Count number of publications
PREFIX dblp: <https://dblp.org/rdf/schema#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
SELECT (COUNT(DISTINCT ?pub) AS ?count) WHERE {
  VALUES (?conference ?stream) {
    ("CRYPTO" <https://dblp.org/streams/conf/crypto>)
    ("EUROCRYPT" <https://dblp.org/streams/conf/eurocrypt>)
	("TCC" <https://dblp.org/streams/conf/tcc>)
  }
  ?pub a dblp:Publication ;
       dblp:publishedInStream ?stream .
}


### Get the i-th element (change offset; index starts at 0)
PREFIX dblp: <https://dblp.org/rdf/schema#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
SELECT DISTINCT ?pub WHERE {
  VALUES (?conference ?stream) {
    ("CRYPTO" <https://dblp.org/streams/conf/crypto>)
    ("EUROCRYPT" <https://dblp.org/streams/conf/eurocrypt>)
	("TCC" <https://dblp.org/streams/conf/tcc>)
  }
  ?pub a dblp:Publication ;
       dblp:publishedInStream ?stream .
}
ORDER BY ?pub 
LIMIT 1
OFFSET 1


### Search with this
PREFIX dblp: <https://dblp.org/rdf/schema#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
SELECT DISTINCT ?pub ?title (GROUP_CONCAT(?authorName; SEPARATOR=", ") AS ?authors) WHERE {
  VALUES (?conference ?stream) {
    ("CRYPTO" <https://dblp.org/streams/conf/crypto>)
    ("EUROCRYPT" <https://dblp.org/streams/conf/eurocrypt>)
	("TCC" <https://dblp.org/streams/conf/tcc>)
  }
  ?pub a dblp:Publication ;
       dblp:publishedInStream ?stream ;
       dblp:title ?title ;
       dblp:authoredBy ?author .
  ?author rdfs:label ?authorName .
}
GROUP BY ?pub ?title
HAVING (
  CONTAINS(
    LCASE(CONCAT(STR(?title), " ", GROUP_CONCAT(?authorName; SEPARATOR=", "))),
    LCASE("bitansky")
  ) &&
  CONTAINS(
    LCASE(CONCAT(STR(?title), " ", GROUP_CONCAT(?authorName; SEPARATOR=", "))),
    LCASE("vinod")
  )
)
ORDER BY ?title
LIMIT 10